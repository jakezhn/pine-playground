//@version=6
indicator(title='wave-trend', overlay=false)

// Inputs
channelLength = input(8, 'Channel Length')
averageLength = input(16, 'Average Length')
lookbackLeftInput = input.int(5, 'Lookback Left', minval=1)
lookbackRightInput = input.int(5, 'Lookback Right', minval=1)
rangeLowerInput = input.int(5, 'Range Lower', minval=1)
rangeUpperInput = input.int(60, 'Range Upper', minval=1)
calculateDivergence = input(true, 'Divergence')
showBarColor = input(true, 'Candle Colours')
showBBSignals = input(true, 'Potential TP Signals')
bullcolour = input.color(color.rgb(0,255,0), title = 'Bull/Bear', inline = 'peb')
bearcolour = input.color(color.rgb(255,0,0), title = '', inline = 'peb')

// Cached constants
var float waveIndexScale = 0.015
var float epsilonVol = 1e-10
var color neutralShade = color.rgb(192, 192, 192, 50)
var color neutralShadeStrong = color.rgb(192, 192, 192, 75)

// WaveTrend Logic
closedprice = close
smoothedPrice = ta.ema(closedprice, channelLength)
priceVolatility = ta.ema(math.abs(closedprice - smoothedPrice), channelLength)
volDenom = math.max(priceVolatility, epsilonVol)
waveIndex = (closedprice - smoothedPrice) / (waveIndexScale * volDenom)
waveTrend = ta.ema(waveIndex, averageLength)
waveTrendSmoothed = ta.sma(waveTrend, 4)
trendChange = ta.cross(waveTrend, waveTrendSmoothed)
isWaveBull = waveTrend > waveTrendSmoothed
waveLine = plot(waveTrend, "Wave Trend", color=color.rgb(33, 150, 243,100), linewidth=1)
signalLine = plot(waveTrendSmoothed, "Signal Line", color=color.rgb(255, 82, 82,100), linewidth=1)
fill(waveLine, signalLine, color = isWaveBull ? bullcolour : bearcolour) 

// Candle Colour Logic 
barColor = isWaveBull ? bullcolour : bearcolour
barcolor(showBarColor ? barColor : na)

// Bollinger Bands Logic
bbSource = waveTrend
bbMiddle = ta.sma(bbSource, 34)
bbDeviation = 2.0 * ta.stdev(bbSource, 34)
bbUpper = bbMiddle + bbDeviation
bbLower = bbMiddle - bbDeviation
bbOffset = 0
waveAboveUpper = waveTrend > bbUpper
signalAboveUpper = waveTrendSmoothed > bbUpper
waveBelowLower = waveTrend < bbLower
signalBelowLower = waveTrendSmoothed < bbLower
bearishBBSignal = waveAboveUpper and signalAboveUpper
bullishBBSignal = waveBelowLower and signalBelowLower
bullishBBCross = bullishBBSignal and not bullishBBSignal[1]
bearishBBCross = bearishBBSignal and not bearishBBSignal[1]
bgcolor(bearishBBSignal ? neutralShade : bullishBBSignal ? neutralShade : na)
plotshape(showBBSignals and bullishBBCross, title="Bullish BB Signal", style=shape.xcross, location=location.belowbar, color=bullcolour, size=size.tiny, text="", textcolor=color.white, force_overlay=true)
plotshape(showBBSignals and bearishBBCross, title="Bearish BB Signal", style=shape.xcross, location=location.abovebar, color=bearcolour, size=size.tiny, text="", textcolor=color.white, force_overlay=true)

// Overbought And Sold Logic
midLinePlot = plot(0, color = na, editable = false, display = display.none)
fill(waveLine, midLinePlot, -60, -80, top_color = color.new(bullcolour, 100), bottom_color = color.new(bullcolour, 0),  title = "Overbought Gradient Fill")
fill(signalLine, midLinePlot, 80,  60,  top_color = color.new(bearcolour, 0), bottom_color = color.new(bearcolour, 100),      title = "Oversold Gradient Fill")
lol1 = hline(-60,na,neutralShade)
hline(-80,na,neutralShade)
lol = hline(60,na,neutralShade)
hline(80,na,neutralShade)
fill(lol1,lol, neutralShadeStrong)
hline(-40, "Oversold", color=neutralShade)
hline(-60, "Oversold", color=neutralShade)
hline(40, "Overbought", color=neutralShade)
hline(60, "Overbought", color=neutralShade)

// Divergence configuration
lookbackLeft = lookbackLeftInput
lookbackRight = lookbackRightInput
rangeLower = rangeLowerInput
rangeUpper = rangeUpperInput
noneColor = color.new(color.white, 100)

_inRange(bool cond) =>
    bars = ta.barssince(cond)
    rangeLower <= bars and bars <= rangeUpper

plFound = false
phFound = false

bullCond = false
bearCond = false
bullCondPlus = false
bearCondPlus = false

waveTrendL = waveTrend[lookbackRight]

if calculateDivergence
    // Regular Bullish
    plFound := not na(ta.pivotlow(waveTrend, lookbackLeft, lookbackRight))    
    prevWavePivotLow = ta.valuewhen(plFound, waveTrendL, 1)
    waveTrendHL = waveTrendL > prevWavePivotLow and _inRange(plFound[1])
    lowL = low[lookbackRight]
    prevPricePivotLow = ta.valuewhen(plFound, low[lookbackRight], 1)
    priceLL = lowL < prevPricePivotLow
    bullCond := priceLL and waveTrendHL and plFound
    pivotValue = prevWavePivotLow
    bullCondPlus := bullCond and pivotValue < -60

    // Regular Bearish
    phFound := not na(ta.pivothigh(waveTrend, lookbackLeft, lookbackRight))
    prevWavePivotHigh = ta.valuewhen(phFound, waveTrendL, 1)
    waveTrendLH = waveTrendL < prevWavePivotHigh and _inRange(phFound[1])
    highL = high[lookbackRight]
    prevPricePivotHigh = ta.valuewhen(phFound, high[lookbackRight], 1)
    priceHH = highL > prevPricePivotHigh
    bearCond := priceHH and waveTrendLH and phFound
    pivotValueBear = prevWavePivotHigh
    bearCondPlus := bearCond and pivotValueBear > 60

// Plots divergence on indicator pane
plot(
     plFound ? waveTrendL : na,
     offset=-lookbackRight,
     title="Regular Bullish",
     linewidth=2,
     color=(bullCond ? bullcolour : noneColor),
     display = display.pane
     )

plot(
     phFound ? waveTrendL : na,
     offset=-lookbackRight,
     title="Regular Bearish",
     linewidth=2,
     color=(bearCond ? bearcolour : noneColor),
     display = display.pane
     )
plotshape(bullCond and not bullCondPlus, title="Bullish Divergence", style=shape.labelup, location=location.belowbar, color=bullcolour, size=size.tiny, text="▲", textcolor=color.white, force_overlay=true)
plotshape(bearCond and not bearCondPlus, title="Bearish Divergence", style=shape.labeldown, location=location.abovebar, color=bearcolour, size=size.tiny, text="▼", textcolor=color.white, force_overlay=true)
plotshape(bullCondPlus, title="Bullish Divergence Plus", style=shape.labelup, location=location.belowbar, color=bullcolour, size=size.small, text="▲+", textcolor=color.white, force_overlay=true)
plotshape(bearCondPlus, title="Bearish Divergence Plus", style=shape.labeldown, location=location.abovebar, color=bearcolour, size=size.small, text="▼+", textcolor=color.white, force_overlay=true)
